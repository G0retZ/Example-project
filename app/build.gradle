apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = new File(System.properties['user.home'], "cargopull.properties")
if (!keystorePropertiesFile.exists()) {
    keystorePropertiesFile = rootProject.file("cargopull.properties")
}

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        upload {
            storeFile file(System.properties['user.home'] + "/" + keystoreProperties['storeFile'])
            keyAlias keystoreProperties['executorAlias']
            keyPassword keystoreProperties['executorPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    buildToolsVersion '27.0.3'
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.cargopull.executor_driver"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 5
        versionName "1.1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'BASE_URL', '"http://tst.capsrv.xyz:9080/executor/"'
        buildConfigField 'String', 'SOCKET_URL', '"ws://tst.capsrv.xyz:9080/executor/ws"'
        buildConfigField 'String', 'GOLOCATION_DESTINATION', '"/mobile/online"'
        buildConfigField 'String', 'CONFIRM_OFFER_DESTINATION', '"/mobile/order"'
        buildConfigField 'String', 'SET_STATUS_DESTINATION', '"/mobile/status"'
        buildConfigField 'String', 'TRIP_DESTINATION', '"/mobile/trip"'
        buildConfigField 'String', 'ROUTE_DESTINATION', '"/mobile/changeRoutePoint"'
        buildConfigField 'String', 'CANCEL_ORDER_DESTINATION', '"/mobile/takeOffOrder"'
        buildConfigField 'String', 'STATUS_DESTINATION', '"/queue/%s"'
        buildConfigField 'String', 'POLLING_DESTINATION', '"/mobile/retrieveOverPackage"'
        buildConfigField 'String', 'STATIC_MAP_KEY', '"AIzaSyC20FZNHJqrQH5UhypeUy3thpqII33QBPI"'
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'BASE_URL', '"https://gate.cargopull.host:8443/executor/"'
            buildConfigField 'String', 'SOCKET_URL', '"wss://gate.cargopull.host:8443/executor/ws"'
            signingConfig signingConfigs.upload
        }
        staging {
            initWith(release)
            buildConfigField 'String', 'BASE_URL', '"http://stg.capsrv.xyz:8080/executor/"'
            buildConfigField 'String', 'SOCKET_URL', '"ws://stg.capsrv.xyz:8080/executor/ws"'
            applicationIdSuffix ".staging"
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Support Library
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    // Android Architecture Components : Lifecycle
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    // Time
    implementation 'joda-time:joda-time:2.9.9'
    // Picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'
    // Reactive Extensions
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    // WebSocket STOMP
    implementation 'com.github.G0retZ:StompProtocolAndroid:18.0.15'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.5.5'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.5.5'
    // Dependency injection
    implementation 'javax.inject:javax.inject:1'
    // Google Play Services
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    // FCM
    implementation 'com.google.firebase:firebase-messaging:17.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    // Analytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'
    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
