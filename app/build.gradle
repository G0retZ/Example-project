apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            keyAlias "androiddebugkey"
            keyPassword "android"
            storePassword "android"
        }
        upload {
            storeFile file(System.getenv('STORAGE_FILE').toString())
            keyAlias System.getenv('EXECUTOR_ALIAS')
            keyPassword System.getenv('EXECUTOR_PASSWORD')
            storePassword System.getenv('STORAGE_PASSWORD')
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.cargopull.executor_driver"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 20
        versionName "1.8.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'QUIET_CHANNEL_ID', '"state_channel"'
        buildConfigField 'String', 'ANNOUNCEMENT_CHANNEL_ID', '"announcement_channel"'
        buildConfigField 'String', 'BASE_URL', '"http://tst.capsrv.xyz:9080/executor/"'
        buildConfigField 'String', 'SOCKET_URL', '"ws://tst.capsrv.xyz:9080/executor/ws"'
        buildConfigField 'String', 'GOLOCATION_DESTINATION', '"/mobile/online"'
        buildConfigField 'String', 'CANCEL_ORDER_DESTINATION', '"/mobile/takeOffOrder"'
        buildConfigField 'String', 'STATUS_DESTINATION', '"/queue/%s"'
        buildConfigField 'String', 'POLLING_DESTINATION', '"/mobile/retrieveOverPackage"'
        buildConfigField 'String', 'STATIC_MAP_KEY', '"AIzaSyBwlubLyqI6z_ivfAWcTCfyTXkoRHTagMk"'
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'BASE_URL', '"https://gate.cargopull.host:8443/executor/"'
            buildConfigField 'String', 'SOCKET_URL', '"wss://gate.cargopull.host:8443/executor/ws"'
            signingConfig signingConfigs.upload
        }
        staging {
            initWith(release)
            buildConfigField 'String', 'BASE_URL', '"http://stg.capsrv.xyz:8080/executor/"'
            buildConfigField 'String', 'SOCKET_URL', '"ws://stg.capsrv.xyz:8080/executor/ws"'
            applicationIdSuffix ".staging"
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            debuggable true
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Support Library
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    // Android Architecture Components : Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.0.0"
    // Time
    implementation 'joda-time:joda-time:2.9.9'
    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    // Reactive Extensions
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.2'
    // WebSocket STOMP
    implementation 'com.github.G0retZ:StompProtocolAndroid:18.0.17'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.5.5'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.5.5'
    // Dependency injection
    implementation 'javax.inject:javax.inject:1'
    // Google Play Services
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    // FCM
    implementation 'com.google.firebase:firebase-messaging:17.6.0'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    // Analytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.24.0'
    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'androidx.annotation:annotation:1.0.2'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
